!--------------------------------------------------
!- Castle Dungeon - Program 2
!- Save on DISK as CD2.PRG -- together with CD1.PRG
!--------------------------------------------------
1 PRINTCHR$(147);CHR$(144):S1=36874:S2=S1+1:S3=S1+2:S4=S1+3:V=S1+4:SB=S1+5:CL=S1-5
2 C=30720:L=7680:MW=59:FC=0:PC=33:BT=8182:FV=15:FP=0:CS=0:POKESB,8:R=37154:AF=0:KF=0
3 POKEV,15:POKECL,255:FORJ=LTOL+505:POKEJ+C,0:POKEJ,32:NEXT
4 READD:IFD=-1THEN6
5 POKEL+D,MW:L=L+D:GOTO4
6 L=7680:FORJ=1TO9:READD:POKEL+D,34:NEXT
7 FORJ=1TO46:READD:POKEL+D,33:NEXT
8 FORJ=1TO3:GOSUB67:POKEB+L,63:NEXT
9 FORJ=1TO9:GOSUB67:POKEB+L,36:NEXT
10 PC=32:GOSUB67:POKEB+L,61
11 GOSUB67:POKEB+L,35
12 FORJ=1TO3:GOSUB67:POKEB+L,60:NEXT
13 GOSUB67:M=B+L:TI$="000000"
14 POKER,127:JS=(PEEK(37137)AND28)OR(PEEK(37152)AND128):JS=ABS(JS-100)/4-7:POKER,255
15 IFTI$>"000500"THEN48
16 IFJS=6THENDR=-22
17 IFJS=5THENDR=22
18 IFJS=3THENDR=-1
19 IFJS=11THENDR=1
20 IFJS=7THENDR=0
21 P=PEEK(DR+M):IFP=59THENDR=0
22 IFP=35THENCS=1:POKEBT,35:POKEBT+C,5:BT=BT+1
23 IFP=36THEN78
24 IFP=34ANDKF=0THENGOSUB73
25 IFP=61THENKF=1:POKE8181,61:POKE8181+C,5
26 GETL$:IFL$="l"THENLS=1:GOSUB82
27 IFP=60ANDLS<>1THENFP=1
28 IFP=60ANDLS=1THENFP=2:PIT=M+DR
29 IFP=63THENAF=AF+1:POKEBT,63:POKEBT+C,5:BT=BT+1:GOSUB75
30 IFDR=0ANDFC=1THEN44
31 POKEM,32:POKEM+C,7:POKEM+DR+C,7:POKEM+DR,58
32 POKEM+DR+C-22,7:POKEM+DR+C+22,7:POKEM+DR+C+1,7:POKEM+DR+C-1,7
33 POKEM+DR+C-23,7:POKEM+DR+C+23,7:POKEM+DR+C+21,7:POKEM+DR+C-21,7:FC=1
34 IFP=35ORP=61THENGOSUB70
35 IFFP=1THENPOKEM+DR,60:GOTO76
36 IFPS=1THENPOKEPIT,60:PS=0
37 IFFP=2THENPS=1:FP=0
38 LS=0:IFDR=0THEN44
39 POKES3,240:FORT=1TO2:NEXT:POKES3,0
40 IFDR=-22THENPOKEM+C+23,0:POKEM+C+22,0:POKEM+C+21,0:GOTO44
41 IFDR=1THENPOKEM+C-23,0:POKEM+C-1,0:POKEM+C+21,0:GOTO44
42 IFDR=-1THENPOKEM+C-21,0:POKEM+C+1,0:POKEM+C+23,0:GOTO44
43 POKEM+C-23,0:POKEM+C-22,0:POKEM+C-21,0
44 M=M+DR:FC=1:IFAF=3THEN46
45 GOTO14
46 POKESB,27:FORT=1TO3000:NEXT:POKECL,240:PRINTCHR$(147):PRINT" you saved the castle"
47 PRINT:PRINT:PRINT" play again?":GOTO52
48 POKES4,220:FORJ=15TO0STEP-.5:POKESB,127:POKEV,J:FORT=1TO10:NEXT
49 POKESB,42:FORT=1TO10:NEXT:NEXT:POKES4,0
50 FORT=1TO5000:NEXT
51 POKECL,240:PRINTCHR$(147):PRINT"play again? y/n"
52 GETA$:IFA$=""THEN52
53 IFA$="y"THENRESTORE:GOTO1
54 IFA$<>"n"THEN52
55 END
56 DATA0,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,3,4,6,8,1,3,4,2,1,1,2,2,1,1,1,1,2
57 DATA1,2,1,1,1,2,2,4,2,4,2,1,2,5,2,2,1,1,2,1,3,2,1,2,3,2,2,3,4,2,1,2,1,5,2,2,1,2,4,5
58 DATA1,1,1,1,1,1,2,3,2,1,1,1,1,2,1,1,1,1,7,3,6,2,3,1,2,1,1,2,1,1,1,1,2,1,1,1,1,2,3,1
59 DATA2,6,2,2,4,2,1,2,1,2,3,3,4,2,7,1,2,1,1,1,1,1,1,1,1,4,1,2,1,1,2,1,12,2,3,2,2,1,1,1
60 DATA2,1,2,2,1,2,2,3,2,2,1,2,2,3,3,2,1,1,1,1,1,2,2,1,2,2,1,1,1,3,3,4,2,2,1,4,3,1,1,1
61 DATA1,2,2,1,1,4,1,2,2,9,6,2,1,2,1,1,1,1,1,1,1,1,1,2,1,1,2,1,1,1,1,1,4,2,5,2,8,1,6
62 DATA3,4,3,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,-1
63 DATA67,72,102,127,140,240,280,316,419
64 DATA23,24,26,27,28,45,46,48,49,50,82,83,84,105,106,123,124,125,145,146,147,162,163
65 DATA184,185,195,196,217,218,301,302,323,324,338,339,360,361,441,442,443,445
66 DATA463,464,465,466,467
67 B=INT(RND(1)*501)+0
68 IFPEEK(B+L)<>PCTHEN67
69 RETURN
70 POKES3,185:POKES2,202:POKES1,202:FORJ=15TO0STEP-.5:POKEV,J:NEXT:POKES3,210
71 POKES1,232:POKES2,232:FORJ=15TO0STEP-.05:POKEV,J:NEXT
72 FORJ=0TO2:POKES1+J,0:NEXT:POKEV,15:RETURN
73 POKEM,32:POKEM+DR,37:POKES4,130:FORJ=15TO0STEP-1:POKEV,J:NEXT:POKES4,0:POKEM+DR,34
74 POKEM,58:POKEV,15:DR=0:FORT=1TO500:NEXT:RETURN
75 POKES3,220:FORT=1TO50:NEXT:POKES3,0:RETURN
76 FORJ=254TO180STEP-.5:POKES2,J:POKES3,J:POKEV,FV:FV=FV-.1:NEXT:POKES2,0:POKES3,0
77 POKECL,240:PRINTCHR$(147):PRINT" you fell into a pit":GOTO81
78 FORG=190TO235:POKES4,G:FORT=1TO10:NEXT:NEXT:FORG=235TO220STEP-1:POKES4,G:FORT=1TO20
79 NEXT:NEXT:FORJ=15TO5STEP-.1:POKEV,J:NEXT:POKES4,0:POKEV,15:IFCS=1THEN24
80 POKECL,240:PRINTCHR$(147):PRINT" you lost to a beast!!"
81 POKESB,27:FORT=1TO3000:NEXT:GOTO51
82 FORI=0TO3:FORJ=0TO15:POKEV,J:N=180+I*J:POKES3,N:NEXTJ,I:POKES3,0:RETURN
